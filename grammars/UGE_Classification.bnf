<classifier> ::= <log_expr>

<log_expr> ::= <or_expr>

<or_expr> ::= <and_expr>
            | <and_expr> "or" <or_expr>

<and_expr> ::= <unary_expr>
             | <unary_expr> "and" <and_expr>

<unary_expr> ::= "not" "(" <log_expr> ")"
               | "(" <log_expr> ")"
               | <comparison>
               | <boolean_feature>

<comparison> ::= <num_expr> <comp_op> <num_expr>

<comp_op> ::= "&lt;" | "&lt;=" | ">" | ">=" | "==" | "!="

<num_expr> ::= <addsub>

<addsub> ::= <muldiv>
           | <muldiv> "+" <addsub>
           | <muldiv> "-" <addsub>

<muldiv> ::= <atom>
           | <atom> "*" <muldiv>
           | <atom> "/" <muldiv>
           | "safe_div" "(" <num_expr> "," <num_expr> ")"

<atom> ::= "(" <num_expr> ")"
         | <numeric_feature>
         | <constant>

<numeric_feature> ::= "x[" <int> "]"
<boolean_feature> ::= "b[" <int> "]"

<constant> ::= <int> "." <int>
             | <int>

<int> ::= <digit>
        | <digit> <int>

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
